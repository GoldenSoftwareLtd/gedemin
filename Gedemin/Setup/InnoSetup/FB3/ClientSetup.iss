; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=Гедымин: Комплексная автоматизация
AppVerName=Гедымин: Комплексная автоматизация
AppID=GedLocal
AppPublisher=Golden Software of Belarus, Ltd
AppPublisherURL=http://www.gsbelarus.com
AppSupportURL=http://www.gsbelarus.com
AppUpdatesURL=http://www.gsbelarus.com
AppSupportPhone=+375-17-2921333, +375-17-3313546
DefaultDirName={pf}\Golden Software\Gedemin
DefaultGroupName=Golden Software Гедымин
DisableProgramGroupPage=yes
OutputDir=d:\temp\setup
OutputBaseFilename=setup
Compression=lzma/ultra
SolidCompression=yes
MinVersion=0,5.0
Uninstallable=yes
ShowLanguageDialog=auto
SourceDir=D:\Golden\Gedemin_Local\
UsePreviousAppDir=yes
DisableReadyPage=yes

[Languages]
Name: "english"; MessagesFile: "compiler:default.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags:
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "gedemin.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "gds32.dll"; DestDir: "{app}"; Flags: ignoreversion
;Source: "gedemin.jpg"; DestDir: "{app}"; Flags: ignoreversion
Source: "gsdbquery.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "ib_util.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "msvcr70.dll"; DestDir: "{app}"
Source: "midas.dll"; DestDir: "{sys}"; Flags: regserver onlyifdoesntexist sharedfile

Source: "Help\fr24rus.chm"; DestDir: "{app}\Help"; Flags: ignoreversion
Source: "Help\vbs55.chm"; DestDir: "{app}\Help"; Flags: ignoreversion

Source: "sct10en.exe"; DestDir: "{app}"; OnlyBelowVersion: 0, 5.01sp2; Flags: ignoreversion deleteafterinstall

[Icons]
Name: "{group}\{code:GetSafeAppName}"; Filename: "{app}\gedemin.exe"; WorkingDir: "{app}"
Name: "{group}\www.gsbelarus.com"; Filename: "http://www.gsbelarus.com"; IconFileName: "{app}\gedemin.exe"
Name: "{commondesktop}\{code:GetSafeAppName}"; Filename: "{app}\gedemin.exe"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{code:GetSafeAppName}"; Filename: "{app}\gedemin.exe"; Tasks: quicklaunchicon
Name: "{group}\{cm:UninstallProgram,{code:GetSafeAppName}}"; Filename: "{uninstallexe}"; WorkingDir: "{app}"

[Registry]
Root: HKLM; SubKey: "SOFTWARE\Golden Software\Gedemin\Client\CurrentVersion\Access"; ValueType: string; ValueName: "UserName"; ValueData: ""; Flags: deletevalue uninsdeletevalue
Root: HKLM; SubKey: "SOFTWARE\Golden Software\Gedemin\Client\ExecuteFiles"; ValueType: dword; ValueName: "{app}\gedemin.exe"; ValueData: 0; Flags: deletevalue uninsdeletevalue
Root: HKLM; SubKey: "Software\Golden Software"; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: "SOFTWARE\Golden Software\Gedemin\Client\CurrentVersion\Setting"; Flags: uninsdeletekey
Root: HKCU; SubKey: "SOFTWARE\Golden Software\Gedemin\Client\CurrentVersion\Access\{code:GetRegAccessSubKey}"; Flags: deletekey uninsdeletekey
Root: HKCU; SubKey: "Software\Golden Software\Gedemin\Client"; Flags: uninsdeletekey
Root: HKCU; SubKey: "Software\Golden Software\Gedemin"; Flags: uninsdeletekeyifempty
Root: HKCU; SubKey: "Software\Golden Software"; Flags: uninsdeletekeyifempty

[Run]
FileName: "{app}\sct10en.exe"; Parameters: "/Q"; WorkingDir: {app}; StatusMsg: "Установка Windows Script Control..."; Flags: skipifdoesntexist
Filename: "regsvr32.exe"; Parameters: """{app}\gsdbquery.dll"""; StatusMsg: "Регистрация библиотеки gsdbquery.dll..."; Flags: skipifdoesntexist
Filename: "{app}\gedemin.exe"; Description: "{cm:LaunchProgram,{code:GetSafeAppName}}"; WorkingDir: {app}; Flags: nowait postinstall skipifsilent

[Code]
function GetSafeAppName(Param: String): String;
begin
  Result := 'Комплексная автоматизация';
end;

function GetRegAccessSubKey(Param: String): String;
begin
  Result := GetSafeAppName('');
end;
